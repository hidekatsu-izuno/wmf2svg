plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

group = 'net.arnx'
version = '0.9.11'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
	testImplementation 'junit:junit:4.12'
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir 'src/stub/java'
		}
	}
}

compileJava {
	options.encoding = 'UTF-8'
}

compileTestJava {
	options.encoding = 'UTF-8'
}

jar {
	manifest {
        attributes 'Main-Class': 'net.arnx.wmf2svg.Main'
    }
	exclude("android/")
	exclude("com/")
}

javadoc {
	options {
		links(JavaVersion.current().isJava11Compatible() ? 
			'https://docs.oracle.com/en/java/javase/11/docs/api' : 
			'https://docs.oracle.com/javase/8/docs/api/')
		locale = 'en_US'
		encoding = 'UTF-8'
		docEncoding = 'UTF-8'
		charSet = 'UTF-8'
		author = true
	}
	options.addBooleanOption('html5', 
		JavaVersion.current().isJava9Compatible())
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

			pom {
				name = 'wmf2svg'
				description = 'WMF to SVG Converting Tool & Library'
				url = 'http://hidekatsu-izuno.github.io/wmf2svg/'

				licenses {
					license {
						name = 'The Apache Software License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id = 'hidekatsu.izuno'
						name = 'Hidekatsu Izuno'
						email = 'hidekatsu.izuno@gmail.com'
					}
				}

				scm {
					url = 'https://github.com/hidekatsu-izuno/wmf2svg'
					connection = 'scm:git:git://github.com/hidekatsu-izuno/wmf2svg.git'
					developerConnection = 'scm:git:ssh:git@github.com:hidekatsu-izuno/wmf2svg.git'
				}
            }
		}
	}
	
	repositories {
		maven {
			url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
			credentials {
				username = sonatypeUsername
				password = sonatypePassword
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}